# Maintainer: Phillip Dixon <phil@dixon.gen.nz>
pkgname=can-modules
pkgver=3.16.3
pkgrel=1
_srcname=linux-3.16
_archkernver=${pkgver}-1-ARCH
license=('GPL2')
url='www.kernel.org'
arch=('x86_64')
source=("https://kernel.org/pub/linux/kernel/v3.x/${_srcname}.tar.xz"
        "https://kernel.org/pub/linux/kernel/v3.x/patch-${pkgver}.xz"
        'enable-can.patch')
md5sums=('5c569ed649a0c9711879f333e90c5386'
         '387a93e4833df73217c6b9b92153aa7c'
         '895a87c36104f3ec70664b616d959bba')

prepare() {
  cd ${_srcname}

  # add upstream patch
  echo "Apply linux point release patch."
  patch -p1 -i "${srcdir}/patch-${pkgver}"

  cp "/usr/lib/modules/${_archkernver}/build/Module.symvers" .
  cp "/usr/lib/modules/${_archkernver}/build/.config" .

  make oldconfig

  patch -i "${srcdir}/enable-can.patch"

  make prepare
  make modules_prepare
}

build() {
  cd ${_srcname}

  make SUBDIRS=net/can modules
  make SUBDIRS=drivers/net/can modules
}

package() {
  cd ${_srcname}

  make INSTALL_MOD_PATH="${pkgdir}/usr/lib/modules/extramodules-3.16-ARCH/" SUBDIRS=net/can modules_install
  make INSTALL_MOD_PATH="${pkgdir}/usr/lib/modules/extramodules-3.16-ARCH/" SUBDIRS=drivers/net/can modules_install

}
