# Maintainer: Phillip Dixon <phil@dixon.gen.nz>
pkgname=c720-modules
_srcname=linux-3.16
pkgver=3.16.3
pkgrel=1
_archkernver=${pkgver}-1-ARCH
pkgdesc="Build patched modules to support acer c720."
arch=('i686' 'x86_64')
url="www.kernel.org"
depends=('linux>=3.16' 'linux<3.17')
makedepends=('linux-headers>=3.16' 'linux-headers<3.17')
license=('GPL2')
options=(!strip)
install=c720-modules.install

source=("https://kernel.org/pub/linux/kernel/v3.x/${_srcname}.tar.xz"
        "https://kernel.org/pub/linux/kernel/v3.x/patch-${pkgver}.xz"
        '05-Add-names-of-Haswell-ULT-i2c-busses.patch'
        '06-Add-HP-Chromebook-14.patch'
        '07-Add-Acer-C720.patch'
       )
md5sums=('5c569ed649a0c9711879f333e90c5386'
         '387a93e4833df73217c6b9b92153aa7c'
         'd81602bb9401ef492955433bdd53f2e6'
         'c2384f642b678168e9cfae62a1ff0536'
         'b21a212dfb30f41f1d385786d7a3369f')

prepare() {
  cd ${_srcname}

  # add upstream patch
  echo "Apply linux point release patch."
  patch -p1 -i "${srcdir}/patch-${pkgver}"

  echo "Apply our local patches."
  patch -p1 -i "$srcdir/05-Add-names-of-Haswell-ULT-i2c-busses.patch"
  patch -p1 -i "$srcdir/06-Add-HP-Chromebook-14.patch"
  patch -p1 -i "$srcdir/07-Add-Acer-C720.patch"

  cp "/usr/lib/modules/${_archkernver}/build/Module.symvers" .
  cp "/usr/lib/modules/${_archkernver}/build/.config" .

  make oldconfig

  make prepare
  make modules_prepare
}

build() {
  cd ${_srcname}

  make SUBDIRS=drivers/platform/chrome modules
}

package() {
  install -D -m644 "$srcdir/${_srcname}/drivers/platform/chrome/chromeos_laptop.ko" \
          "${pkgdir}/usr/lib/modules/${_archkernver}/updates/chromeos_laptop.ko"
  gzip "${pkgdir}/usr/lib/modules/${_archkernver}/updates/"*.ko
  sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION='${_archkernver}'/" "${startdir}/c720-modules.install"
}
